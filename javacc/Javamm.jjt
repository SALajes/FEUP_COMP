options {
	LOOKAHEAD = 1;
}

PARSER_BEGIN(Javamm)

import java.io.FileInputStream;

public class Javamm {
    static int numErrors = 0;
    static final int MAX_NUM_ERRORS = 5;
	public static void main(String args[]) throws ParseException {
	    Javamm javamm;
		try{
		    javamm = new Javamm(new java.io.FileInputStream(args[0]));
		    SimpleNode root = javamm.Start();
            root.dump("");
		}catch(Exception e){
		    System.out.println("File not found");
		    System.exit(-1);
		}
	}
}

PARSER_END(Javamm)

SKIP: {
	" " | "\r" | "\t" | "\n"
	| < "//"(~["\n","\r"])*("\n" | "\r" | "\r\n")>
	| < "/*"(~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN: {
  < IMPORT: "import" >
| < CLASS: "class" >
| < EXTENDS: "extends" >
| < PUBLIC: "public" >
| < STATIC: "static" >
| < VOID: "void" >
| < NEW: "new" >
| < LEN: "length" >
| < MAIN: "main" >
| < STRING: "String" >
| < INT: "int" >
| < BOOLEAN: "boolean" >
| < IF: "if" >
| < THEN: "then" >
| < ELSE: "else" >
| < WHILE: "while" >
| < TRUE: "true" >
| < FALSE: "false" >
| < THIS: "this" >
| < LTHAN: "<" >
| < GTHAN: ">" >
| < PLUS: "+" >
| < MINUS: "-" >
| < ASTERISK: "*" >
| < FSLASH: "/" >
| < EQUALS: "=" >
| < AND: "&&" >
| < SEMICOL: ";" >
| < COL: "," >
| < DOT: "." >
| < NOT: "!" >
| < OCURLY: "{" >
| < CCURLY: "}" >
| < OPAREN: "(" >
| < CPAREN: ")" >
| < OSBRACKET: "[" >
| < CSBRACKET: "]" >
| < RETURN: "return" >
| < NUM: ((["0"-"9"])+) >
| < ID: (["A"-"Z", "a"-"z", "_", "$"](["0"-"9", "A"-"Z", "a"-"z", "_", "$"])*) >
}

SimpleNode Start(): { } { 
	( ImportDeclaration() )*
	ClassDeclaration() <EOF> 
	{ return jjtThis; }
}

void ImportDeclaration() : { } {
	<IMPORT> [<STATIC>]
	<ID> [(<DOT> <ID> )* <OPAREN> Type() (<COL> Type())* <CPAREN>]
	[Type()]
	<SEMICOL>
}

void ClassDeclaration() : { } {
	<CLASS> <ID> [ <EXTENDS> <ID> ] <OCURLY>
		( VarDeclaration() )*
		( MethodDeclaration() )*
	<CCURLY>
}

void VarDeclaration()  : { } {
	Type() <ID> <SEMICOL>
}

void MethodDeclaration()  : { } {
	<PUBLIC> ( MainDeclaration() | FunctionDeclaration())
}

void MainDeclaration()  : { } {
	<STATIC> <VOID> <MAIN> <OPAREN> <STRING> <OSBRACKET> <CSBRACKET> <ID> <CPAREN> <OCURLY>
		( LOOKAHEAD(2) VarDeclaration() )*
		( Statement() ) *
	<CCURLY>
}

void FunctionDeclaration()  : { } {
	Type() <ID> <OPAREN> [ Type() <ID> (<COL> Type() <ID> )* ] <CPAREN> <OCURLY>
		( LOOKAHEAD(2) VarDeclaration() )*
		( Statement() )*
		<RETURN> Expression() <SEMICOL>
	<CCURLY>
}

void Type()  : { } {
	(<INT> [<OSBRACKET> <CSBRACKET>])
	|
	<BOOLEAN>
	|
	<ID>
	|
	<VOID>
}

void Statement()  : { } {
	(<OCURLY> (Statement())* <CCURLY>)
	|
	(<IF> <OPAREN> Expression() <CPAREN> Statement() <ELSE> Statement())
	|
	[LOOKAHEAD(2) <ID> StatementId()] (Expression() <SEMICOL>)
	|
	<WHILE> <OPAREN>
	try {
	    (Expression() <CPAREN>) // We only need to check for errors inside the while parenthesis
	} catch(ParseException e) {
        System.out.println(e.toString());  // print the error message
        Token t;
        Javamm.numErrors++; // Increase the number of errors since execution

        if (Javamm.numErrors > 5) { // Chose 5 but this value should be passed through command line maybe?
        System.out.println("Too many errors. Aborting.");
        System.exit(-1);
        }

        do {
          t = getNextToken(); // Skip the tokens until the close of parenthesis
        } while (t.kind != CPAREN); //{, }, ;, )
          // The above loop consumes tokens all the way up to a token of
          // "kind".  We use a do-while loop rather than a while because the
          // current token is the one immediately before the erroneous token
          // (in our case the token immediately before what should have been
          // "if"/"while".
	}
	Statement()
}

void StatementId()  : { } {
	<EQUALS>
	|
	(<OSBRACKET> Expression() <CSBRACKET> <EQUALS>)
}

void Expression()  : { } {
	ExpressionTerminal() ( LOOKAHEAD(2) ExpressionL())?
 }

 void ExpressionL()  : { } {
 	(<AND> | <LTHAN> | <PLUS> | <MINUS> | <ASTERISK> | <FSLASH>) Expression()
 	|
 	(<OSBRACKET> Expression() <CSBRACKET>)
 	|
 	ExpressionDot()
 }

 void ExpressionDot()  : { } {
 	<DOT> ExpressionDotL()
 }

 void ExpressionDotL()  : { } {
 	<LEN>
 	|
 	(<ID> <OPAREN> [Expression() (<COL> Expression())*] <CPAREN>)
 }

 void ExpressionTerminal()  : { } {
 	<NUM>
 	|
 	<TRUE>
 	|
 	<FALSE>
 	|
 	<ID>
 	|
 	<THIS>
 	|
 	<NOT> Expression()
 	|
 	ExpressionNew()
 	|
 	(<OPAREN> Expression() <CPAREN>)
 }

 void ExpressionNew()  : { } {
 	<NEW> ExpressionNewL()
 }

void ExpressionNewL()  : { } {
	(<INT> <OSBRACKET> Expression() <CSBRACKET>)
	|
	(<ID> <OPAREN> <CPAREN>)
}