options {
	LOOKAHEAD = 1;
	STATIC = false;
	MULTI = true;
}

PARSER_BEGIN(Javamm)

import java.io.FileInputStream;
import java.io.FileNotFoundException;

public class Javamm {
    static final int MAX_NUM_ERRORS = 16;

    private int numErrors = 0;

    public int getNumErrors(){
        return numErrors;
    }
}

PARSER_END(Javamm)

SKIP: {
	" " | "\r" | "\t" | "\n"
	| < "//"(~["\n","\r"])*("\n" | "\r" | "\r\n")>
	| < "/*"(~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
}

TOKEN: {
  < IMPORT: "import" >
| < CLASS: "class" >
| < EXTENDS: "extends" >
| < PUBLIC: "public" >
| < STATIC: "static" >
| < VOID: "void" >
| < NEW: "new" >
| < LEN: "length" >
| < MAIN: "main" >
| < STRING: "String" >
| < INT: "int" >
| < BOOLEAN: "boolean" >
| < IF: "if" >
| < THEN: "then" >
| < ELSE: "else" >
| < WHILE: "while" >
| < TRUE: "true" >
| < FALSE: "false" >
| < THIS: "this" >
| < LTHAN: "<" >
| < GTHAN: ">" >
| < PLUS: "+" >
| < MINUS: "-" >
| < ASTERISK: "*" >
| < FSLASH: "/" >
| < EQUALS: "=" >
| < DEQUALS: "==" >
| < AND: "&&" >
| < SEMICOL: ";" >
| < COL: "," >
| < DOT: "." >
| < NOT: "!" >
| < OCURLY: "{" >
| < CCURLY: "}" >
| < OPAREN: "(" >
| < CPAREN: ")" >
| < OSBRACKET: "[" >
| < CSBRACKET: "]" >
| < RETURN: "return" >
| < NUM: ((["0"-"9"])+) >
| < ID: (["A"-"Z", "a"-"z", "_", "$"](["0"-"9", "A"-"Z", "a"-"z", "_", "$"])*) >
}

SimpleNode Start() : { } {
	( ImportDeclaration() )*
	ClassDeclaration() <EOF> 
	{ return jjtThis; }
}

void ImportDeclaration() #Import : { Token id; } {
	<IMPORT>  [<STATIC> ]
	id=<ID> { jjtThis.identity=id.image; } [ ImportDeclarationDot() ]
	[ <VOID> | Type() ]
	<SEMICOL>
}

void ImportDeclarationDot() : { String type; Token id; } {
    (<DOT> id=<ID> { jjtThis.identity = id.image; }  ImportDeclarationDot())
    |
    <OPAREN> [ <VOID> | ( type=Type() (<COL> Type())* ) { jjtThis.type = type; } ] <CPAREN>
}

void ClassDeclaration() #Class : { Token class_id, extend_class; } {
	<CLASS> class_id=<ID> { jjtThis.identity=class_id.image; }
	  ( <EXTENDS> extend_class=<ID> { jjtThis.extend=extend_class.image; } #Extends )? <OCURLY>
		( VarDeclaration() )*
		( MethodDeclaration() )*
	<CCURLY>
}

void VarDeclaration() #Var : { String type; Token id; } {
	type=Type() id=<ID> { jjtThis.type=type; jjtThis.identity=id.image; }
	    [ <EQUALS> <OCURLY> (<NUM>|<ID>) (<COL> (<NUM>|<ID>))* <CCURLY> ]<SEMICOL> //curly solve
}

void MethodDeclaration() #void : { } {
	<PUBLIC> ( MainDeclaration() | FunctionDeclaration())
}

void MainDeclaration() #Main : { Token id; } {
	<STATIC> <VOID> <MAIN> <OPAREN> <STRING> <OSBRACKET> <CSBRACKET> id=<ID> { jjtThis.identity=id.image; } <CPAREN> <OCURLY>
		( LOOKAHEAD(2) VarDeclaration() )*
		( Statement() ) *
	<CCURLY>
}

void FunctionDeclaration() #Method : { String type; Token id; } {
	type=Type() id=<ID> { jjtThis.identity=id.image; jjtThis.type=type; } <OPAREN>
	    FunctionArguments() <CPAREN> <OCURLY>
		( LOOKAHEAD(2) VarDeclaration() )*
		( Statement() )*
		<RETURN> Expression() <SEMICOL>
	<CCURLY>
}

void Argument() : { String type; Token id; } {
    type=Type() id=<ID> { jjtThis.type=type; jjtThis.identity=id.image; }
}

void FunctionArguments() #Arguments : { } {
    [ Argument() (<COL> Argument())* ]
}

String Type() : { String return_value; Token id; } {
	<INT> { return_value = "int"; }
	[<OSBRACKET> <CSBRACKET> { return_value = "int[]"; } ]
	{return return_value;}
	|
	<BOOLEAN> { return "boolean"; }
	|
	id=<ID> { return id.image; }
}

void Statement() #void: {} {
	(<OCURLY> (Statement())* <CCURLY>)
	|
	IfStatement()
	|
    WhileStatement()
	|
	( LOOKAHEAD(2)
        VariableInit()
        |
        Expression() <SEMICOL>
	)
}

void IfStatement() #If : {} {
    <IF> <OPAREN> Expression() <CPAREN>
    Statement() //Then 
    <ELSE>  Statement() //else
}

void WhileStatement() #While : {} {
    <WHILE> <OPAREN>
    	try {
    	    (Expression())* <CPAREN> // We only need to check for errors inside the while parenthesis
    	} catch(ParseException e) {
            System.out.println(e.toString());  // print the error message
            Token t;
            this.numErrors++; // Increase the number of errors since execution

            if (this.numErrors > Javamm.MAX_NUM_ERRORS) {
                System.out.println("Too many errors. Aborting. (" + this.numErrors + ")");
                throw e;
            }

            while(true) {
              t = getToken(1); // Skip the tokens until the close of parenthesis
              if(t.kind == CPAREN || t.kind == CCURLY){
                t = getNextToken();
                t=getToken(1);
                if(t.kind != CPAREN)
                    break;
              }
              else if(t.kind == OCURLY)
                break;
              else t = getNextToken();
            }
    	}
        Statement()
}

void VariableInit() : { Token id; } {
    id=<ID> { jjtThis.identity=id.image; }

    (
        <EQUALS> Expression() #VarInit
        |
        <OSBRACKET> Expression() #Index <CSBRACKET> <EQUALS> Expression() #ArrayInit
    )
    <SEMICOL>
}

void Expression() : { } {
    //We will now dispose operators from lowest to highest precedence:
    // &&, <, + -, * /, new, !, (), ., []
    LessThan() (<AND> LessThan() #And )*
 }

void LessThan() : {} {
    AdditionSubtraction() (<LTHAN> AdditionSubtraction())*
}

void AdditionSubtraction() : {Token operator;} {
    MultiplicationDivision()
    (
        (operator=<PLUS> | operator=<MINUS>) MultiplicationDivision() { jjtThis.identity=operator.image; }
    )*
}

void MultiplicationDivision() : { Token operator; } {
    ExpressionIntermediateDivision()
    (
        (operator=<ASTERISK> | operator=<FSLASH> ) ExpressionTerminal() { jjtThis.identity=operator.image; }
    )*
}

void ExpressionIntermediateDivision() : {} {
    <NOT> ExpressionIntermediateDivision() #Not
    |
    ExpressionTerminal() [<DOT> ExpressionDot()]
}

void ExpressionTerminal() #Terminal : { Token id;} {
    id=<NUM> {jjtThis.identity=id.image; jjtThis.type="int"; } #Integer
    |
    id=<TRUE> {jjtThis.identity=id.image; jjtThis.type="boolean";} #True
    |
    id=<FALSE> {jjtThis.identity=id.image; jjtThis.type="boolean"; } #False
    |
    <THIS> #This
    |
    id=<ID> {jjtThis.identity=id.image;} #ID [ <OSBRACKET> Expression() <CSBRACKET> ]
    |
    <OPAREN> Expression() <CPAREN>
    |
    ExpressionNew()
}

void ExpressionDot() : { Token id; } {
    <LEN> #Length
    |
    (LOOKAHEAD(2) ExpressionTerminal() <DOT>)* id=<ID> <OPAREN> [Expression() (<COL> Expression())*] <CPAREN>
        { jjtThis.identity=id.image; }
}

void ExpressionNew() : { Token return_value; } {
    <NEW>
    (
        <INT> <OSBRACKET> Expression() <CSBRACKET> { jjtThis.returns="int[]"; }
        |
        return_value=<ID> { jjtThis.returns=return_value.image; }  <OPAREN> <CPAREN>
    )
}